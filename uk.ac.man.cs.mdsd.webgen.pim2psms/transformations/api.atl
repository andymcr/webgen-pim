-- @atlcompiler emftvm 
--
-- @path Website=/uk.ac.man.cs.mdsd.webgen.model/model/website.ecore
-- @path Service=/uk.ac.man.cs.mdsd.service.model/model/Service.xecore
-- @path API=/uk.ac.man.cs.mdsd.api.model/model/api.xecore

module API;
create api : API from website : Website, service : Service;

uses WebsiteHelpers;


rule API {
	from website : Website!API
	to api : API!API (
		resources <- website.resources
	)
}


rule Resource {
	from website : Website!Resource
	to api : API!Resource (
		service <-
			if website.service.oclIsUndefined() then
				OclUndefined
			else
				thisModule.findService(website.service.name)
			endif,
		uriElement <-
			if website.uriElement.trim() <> '' then
				website.uriElement.trim()
			else if not website.name.oclIsUndefined() then
				website.name.createName()
			else
				''
			endif endif,
		enableDefaultGetOne <- website.enableDefaultGetOne,
		enableDefaultGetAll <- website.enableDefaultGetAll,
		getAll <- website.getAll.findServiceSelection(),
		getOne <- website.getOne.findServiceSelection(),
		selections <- website.selections,
		defaultSerializationGroups <-
			website.defaultSerializationGroups
				->collect(g | thisModule.findOrmSerializationGroup(g.name)),
		childResources <- website.childResources
	)
} 

rule ResourceSelection {
	from website : Website!ResourceSelection
	to api : API!ResourceSelection (
		selection <- website.selection.findServiceSelection(),
		uriElement <-
			if website.uriElement.trim() <> '' then
				website.uriElement.trim()
			else if not website.name.oclIsUndefined() then
				website.name.createName()
			else
				''
			endif endif,
		defaultSerializationGroups <-
			if website.defaultSerializationGroups->notEmpty() then
				website.defaultSerializationGroups
				->collect(g | thisModule.findOrmSerializationGroup(g.name))
			else
				website.parentResource.defaultSerializationGroups
					->collect(g | thisModule.findOrmSerializationGroup(g.name))
			endif
	)
}