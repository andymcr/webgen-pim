-- @atlcompiler emftvm 
--
-- @path Website=/uk.ac.man.cs.mdsd.webgen.model/model/website.ecore
-- @path ORM=/uk.ac.man.cs.mdsd.orm.model/model/orm.ecore

module PassPersistenceImplicitStatic;
create dummyOrm : ORM refining orm : ORM, website : Website;

uses WebsiteHelpers;


helper context Website!WebGenModel def: getDataType(name : String) : ORM!DataType
	= let dataType : ORM!DataType = thisModule.findOrmDataType(name)
		in if not dataType.oclIsUndefined() then
			dataType
		else
			if name = 'String' then
				thisModule.implicitStringDataType(self)
			else if name = 'TextBlock' then
				thisModule.implicitTextBlockDataType(self)
			else
				OclUndefined
			endif endif
		endif;


rule OrmModel {
	from partialOrm : ORM!OrmModel
	to orm : ORM!OrmModel (
		entitiesAndViews <- partialOrm.entitiesAndViews
			->union(if thisModule.findWebsiteProperties().staticUnitsEditable then
						Sequence{thisModule.implicitStaticEntity(thisModule.findWebGenModel())}
					else
						Sequence{}
					endif)
	)
}

unique lazy rule implicitStringDataType {
	from website: Website!WebGenModel
	to orm : ORM!DataType (
		name <- 'String',
		displayLabel <- 'String'
	)
	do {
		orm;
	}
}

unique lazy rule implicitTextBlockDataType {
	from website: Website!WebGenModel
	to orm : ORM!DataType (
		name <- 'TextBlock',
		displayLabel <- 'Text Block'
	)
	do {
		orm;
	}
}


unique lazy rule implicitStaticEntity {
	from website: Website!WebGenModel
	to orm : ORM!Entity (
		name <- 'StaticText',
		displayLabel <- 'Static Text',
		features <- Sequence{name, content},
		tableName <- 'StaticText'.createTableName(),
		autoKeyName <- 'id'
	),
	name : ORM!SingletonElement (
		name <- 'name',
		displayLabel <- 'Name',
		dataType <- website.getDataType('String'),
		required <- true,
		"unique" <- true,
		columnName <- 'name'.createAttributeColumnName()
	),
	content : ORM!SingletonElement (
		name <- 'content',
		displayLabel <- 'Content',
		dataType <- website.getDataType('TextBlock'),
		required <- true,
		"unique" <- false,
		columnName <- 'content'.createAttributeColumnName()
	)
	do {
		orm;
	}
}
