-- @atlcompiler emftvm 
--
-- @path Website=/uk.ac.man.cs.mdsd.webgen.model/model/website.ecore
-- @path ORM=/uk.ac.man.cs.mdsd.orm.model/model/orm.ecore
-- @path WAF=/uk.ac.man.cs.mdsd.waf.model/model/Waf.ecore

module PassAuthentication;
create dummyWaf : WAF refining waf : WAF, website : Website, orm : ORM;

uses WebsiteHelpers;


rule WafModel {
	from partialWaf : WAF!WafModel
	to waf : WAF!WafModel (
		pages <-
			let websiteProperties : Website!WebsiteProperties
				= thisModule.findWebsiteProperties()
			in partialWaf.pages
				->union(
					if websiteProperties.authentication.oclIsUndefined() then
						Sequence{}
					else if websiteProperties.authentication.oclIsTypeOf(Website!LocalAuthenticationSystem) then
						let authentication : Website!LocalAuthenticationSystem
							= websiteProperties.authentication.oclAsType(Website!LocalAuthenticationSystem)
						in if authentication.loginUnit.oclIsUndefined() then
								Sequence{thisModule.implicitLoginPage(authentication)}
							else
								Sequence{}
							endif
					else
						Sequence{}
					endif endif)
				->union(
					if websiteProperties.authentication.oclIsUndefined() then
						Sequence{}
					else if websiteProperties.authentication.oclIsTypeOf(Website!LocalAuthenticationSystem) then
						let authentication : Website!LocalAuthenticationSystem
							= websiteProperties.authentication.oclAsType(Website!LocalAuthenticationSystem)
						in if authentication.authenticationSource.oclIsUndefined() then
								if authentication.allowSelfRegistration  then
									Sequence{
										thisModule.implicitRegistrationPage(authentication),
										thisModule.implicitManageAuthenticationsPage(authentication)}
								else
									Sequence{
										thisModule.implicitManageAuthenticationsPage(authentication)}
									endif
							else
								Sequence{}
							endif
					else
						Sequence{}
					endif endif),
		authentication <- thisModule.findWebsiteProperties().authentication
	)
}


abstract rule Authentication {
	from website : Website!Authentication
	to waf : WAF!Authentication (
		loginLabel <- website.loginLabel,
		logoutLabel <- website.logoutLabel
	)
}

-- @extends Authentication
rule LocalAuthenticationSystem {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!LocalAuthenticationSystem (
		userService <- thisModule.findWafService(website.userSource.name),
		userAuthenticationKey <-
			thisModule.findWafService(website.userSource.name)
				.features->any(f | f.name = website.userAuthenticationKey.name),
		authenticationService <-
			if not website.authenticationSource.oclIsUndefined() then
				if website.authenticationSource.oclIsTypeOf(Website!Service) then
					website.authenticationSource
				else
					thisModule.findWafService(website.authenticationSource.name)
				endif
			else
				thisModule.findWafService('Authentication')
			endif,
		loginAttemptService <-
			if website.trackLoginAttempts then
				thisModule.findWafService('LoginAttempt')
			else
				OclUndefined
			endif,
		allowSelfRegistration <- website.allowSelfRegistration,
		autoLoginService <-
			if website.allowAutoLogin then
				thisModule.findWafService('AutoLogin')
			else
				OclUndefined
			endif,
		useEmailActivation <- website.useEmailActivation,
		sendWelcomeEmail <- website.sendWelcomeEmail,
		registrationUnit <-
			if not website.registrationUnit.oclIsUndefined() then
				website.registrationUnit
			else if website.allowSelfRegistration then
				thisModule.implicitRegistrationUnit(website)
			else
				OclUndefined
			endif endif,
		loginUnit <-
			if not website.loginUnit.oclIsUndefined() then
				website.loginUnit
			else
				thisModule.implicitLoginUnit(website)
			endif,
		forgottenPasswordUnit <-
			if not website.forgottenPasswordUnit.oclIsUndefined() then
				website.forgottenPasswordUnit
			else if not website.loginUnit.oclIsUndefined() then
				thisModule.implicitForgottenPasswordUnit(website)
			else
				OclUndefined
			endif endif
	)
}

-- @extends Authentication
rule CasAuthentication {
	from website : Website!CasAuthentication
	to waf : WAF!CasAuthentication (
	)
}


rule AuthenticationService {
	from waf : WAF!Service (waf.name = 'Authentication')
	to dummyWaf : WAF!Service (
		keys <-
			if waf.keys->notEmpty() then
				waf.keys
			else
				Sequence{thisModule.implicitAuthenticationKey(waf)}
			endif
	)
}

lazy rule implicitAuthenticationKey {
	from waf : WAF!Service
	to dummyWaf : WAF!ServiceAttributeReference (
		attribute <- waf.findServiceFeature('email')
	)
}


unique lazy rule implicitRegistrationUnit {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!RegistrationUnit (
		name <- 'registerUser',
		displayLabel <- 'Register User',
		service <- thisModule.findWafService('Authentication'),
		omitCaption <- false,
		confirmDestination <- 
			if not website.loginUnit.oclIsUndefined() then
				website.loginUnit.refImmediateComposite()
			else
				thisModule.implicitLoginPage(website)
			endif,
		confirmLabel <- 'Register',
		displayFields <-
			let includedFeatures : Sequence(WAF!IncludedFeature)
				= thisModule.findWafService('Authentication').features
					->select(f | f.name = 'username' or f.name = 'email' or f.name = 'password')
				in includedFeatures->select(f | f.name = 'username')
					->collect(f | thisModule.implicitUnitElement(f))
				->union(includedFeatures->select(f | f.name = 'email')
					->collect(f | thisModule.implicitUnitElement(f)))
				->union(Sequence{confirmEmail})
				->union(includedFeatures->select(f | f.name = 'password')
					->collect(f | thisModule.implicitUnitElement(f)))
				->union(Sequence{confirmPassword})
				->union(if website.useCaptcha then
						Sequence{thisModule.implicitCaptchaField(website)}
					else
						Sequence{}
					endif),
		styleClass <- 'unit register_unit framed',
		headerClass <- 'unit_header',
		captionClass <- 'unit_caption',
		controlClass <- 'unit_control',
		footerClass <- 'unit_footer',
		errorClass <- 'error',
		layoutClass <- 'input_form'
	),
	confirmEmail : WAF!DataTypeField (
		name <- 'confirmEmail',
		dataType <- thisModule.findOrmDataType('Email'),
		cardinality <- #Required,
		caseInsensitive <- true,
		displayLabel <- 'Confirm Email',
		inputClass <- 'input_attribute'
		),
	confirmPassword : WAF!DataTypeField (
		name <- 'confirmPassword',
		dataType <- thisModule.findOrmDataType('String'),
		cardinality <- #Required,
		obfuscateFormFields <- true,
		encrypt <- true,
		displayLabel <- 'Confirm Password',
		inputClass <- 'input_attribute'
	)
	do {
		waf;
	}
}

unique lazy rule implicitLoginUnit {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!LoginUnit (
		name <- 'Login',
		displayLabel <- 'Login',
		service <- thisModule.findWafService('Authentication'),
		omitCaption <- false,
		submitLabel <- 'Login',
		displayFields <-
			let includedFeatures : Sequence(WAF!IncludedFeature)
				= thisModule.findWafService('Authentication').features
					->select(f | f.name = 'email' or f.name = 'password')
				in includedFeatures->select(f | f.name = 'email')
					->collect(f | thisModule.implicitUnitAttribute(f))
				->union(includedFeatures->select(f | f.name = 'password')
					->collect(f | thisModule.implicitUnitAttribute(f)))
				->union(if website.allowAutoLogin then
						Sequence{thisModule.implicitRememberMeField(website)}
					else
						Sequence{}
					endif)
				->union(if website.useCaptcha then
						Sequence{thisModule.implicitCaptchaField2(website)}
					else
						Sequence{}
					endif),
-- assigned in next pass		loginIdField <- OclUndefined,
-- assigned in next pass		passwordField <- OclUndefined,
-- assigned in next pass		rememberMeField <- rememberMe,
		styleClass <- 'unit login_unit framed',
		headerClass <- 'unit_header',
		captionClass <- 'unit_caption',
		controlClass <- 'unit_control',
		footerClass <- 'unit_footer',
		errorClass <- 'error',
		layoutClass <- 'control_form'
	)
	do {
		waf;
	}
}

unique lazy rule implicitForgottenPasswordUnit {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!ForgottenPasswordUnit (
		name <- 'ForgottenPassword',
		displayLabel <- 'Recover Forgotten Password',
		service <- thisModule.findWafService('Authentication'),
		omitCaption <- false,
		submitLabel <- 'Recover Password',
		displayFields <-
			let includedFeatures : Sequence(WAF!IncludedFeature)
				= thisModule.findWafService('Authentication').features
					->select(f | f.name = 'email')
				in includedFeatures
					->collect(f | thisModule.implicitUnitAttribute(f)),
-- assigned in next pass		loginIdField <- OclUndefined,
		styleClass <- 'unit forgotten_password_unit framed',
		headerClass <- 'unit_header',
		captionClass <- 'unit_caption',
		controlClass <- 'unit_control',
		footerClass <- 'unit_footer',
		errorClass <- 'error',
		layoutClass <- 'control_form'
	)
	do {
		waf;
	}
}


lazy abstract rule implicitIncludedFeature {
	from partialWaf : WAF!IncludedFeature
	to waf : WAF!IncludedFeature (
		displayLabel <- partialWaf.displayLabel,
		headerClass <- partialWaf.headerClass,
		inputClass <- partialWaf.inputClass,
		displayClass <- partialWaf.displayClass,
		footerClass <- partialWaf.footerClass
	)
}

-- @extends implicitIncludedFeature
lazy abstract rule implicitIncludedAttribute {
	from partialWaf : WAF!IncludedAttribute
	to waf : WAF!IncludedAttribute (
		obfuscateFormFields <- partialWaf.obfuscateFormFields,
		dateFormat <- partialWaf.dateFormat
	)
}

-- @extends implicitIncludedFeature
lazy abstract rule implicitIncludedAssociation {
	from partialWaf : WAF!IncludedAssociation
	to waf : WAF!IncludedAssociation (
	)
}

--@extends implicitIncludedFeature
lazy abstract rule implicitUnitFeature {
	from partialWaf : WAF!ServiceFeature
	to waf : WAF!UnitFeature (
		name <- partialWaf.name,
		cardinality <- partialWaf.cardinality,
		onlyDisplayWhenNotEmpty <- false
	)
}

-- @extends implicitUnitFeature, implicitIncludedAttribute
lazy rule implicitUnitAttribute {
	from partialWaf : WAF!ServiceAttribute
	to waf : WAF!UnitElement (
		serviceFeature <- partialWaf
	)
	do {
		waf;
	}
}

unique lazy rule implicitRememberMeField {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!DataTypeField (
		name <- 'rememberMe',
		dataType <- thisModule.findOrmDataType('Boolean'),
		cardinality <- #Optional,
		displayLabel <- 'Remember Me',
		inputClass <- 'input_checkbox'
	)
	do {
		waf;
	}
}

lazy rule implicitCaptchaField {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!CaptchaField (
		name <- 'captcha'
	)
	do {
		waf;
	}
}

lazy rule implicitCaptchaField2 {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!CaptchaField (
		name <- 'captcha'
	)
	do {
		waf;
	}
}

unique lazy rule implicitRegistrationPage {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!Page (
		name <- 'RegisterAuthentication',
		displayLabel <- 'Register Authentication',
		units <- Sequence{thisModule.implicitRegistrationUnit(website)},
		authenticated <- not website.allowSelfRegistration,
		uriElement <- 'register',
		navigationLabel <- 'Register',
		sideMenu <-
			let sideMenu : Website!Menu
				= thisModule.findWebsiteProperties().sideMenu
			in if sideMenu.oclIsUndefined() then
					OclUndefined
				else
					thisModule.findWafMenu(sideMenu.name)
				endif
	)
	do {
		waf;
	}
}

unique lazy rule implicitLoginPage {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!Page (
		name <- 'Authentication',
		displayLabel <- 'Login',
		units <- Sequence{thisModule.implicitLoginUnit(website)}
			->union(
				if website.forgottenPasswordUnit.oclIsUndefined() then
					Sequence{thisModule.implicitForgottenPasswordUnit(website)}
				else
					Sequence{}
				endif),
		authenticated <- false,
		uriElement <- 'login',
		navigationLabel <- 'Login',
		sideMenu <-
			let sideMenu : Website!Menu
				= thisModule.findWebsiteProperties().sideMenu
			in if sideMenu.oclIsUndefined() then
					OclUndefined
				else
					thisModule.findWafMenu(sideMenu.name)
				endif
	)
	do {
		waf;
	}
}

unique lazy rule implicitManageAuthenticationsPage {
	from website : Website!LocalAuthenticationSystem
	to waf : WAF!Page (
		name <- 'ManageAuthentications',
		displayLabel <- 'Manage Authentications',
		units <- Sequence{users, editUser},
		authenticated <- true,
		uriElement <- 'auth',
		topMenuOption <- #IncludeWhenAuthenticated,
		topMenuRank <- 100,
		navigationLabel <- 'Authentications',
		sideMenu <-
			let sideMenu : Website!Menu
				= thisModule.findWebsiteProperties().sideMenu
			in if sideMenu.oclIsUndefined() then
					OclUndefined
				else
					thisModule.findWafMenu(sideMenu.name)
				endif
	),
	users : WAF!IndexGridUnit (
		name <- 'users',
		displayLabel <- 'Users',
		service <- thisModule.findWafService('Authentication'),
		omitCaption <- false,
		defaultPaginationSize <- 10,
		nextPageLabel <- '>',
		previousPageLabel <- '<',
		displayFields <-
			let includedFeatures : Sequence(WAF!IncludedFeature)
				= thisModule.findWafService('Authentication').features
					->select(f | f.name = 'username' or f.name = 'email' or f.name = 'lastLogin')
				in includedFeatures
					->collect(f | thisModule.implicitUnitAttribute(f)),
		actions <- Sequence{editUserAction},
		styleClass <- 'unit index_unit framed',
		headerClass <- 'unit_header',
		captionClass <- 'unit_caption',
		controlClass <- 'unit_control',
		footerClass <- 'unit_footer',
		errorClass <- 'error',
		layoutClass <- 'index_layout',
		rowClasses <- 'odd_row even_row'
	),
	editUserAction : WAF!SelectAction (
		name <- 'edit',
		displayLabel <- 'Edit',
		target <- editUser
	),
	editUser : WAF!UpdateUnit (
		name <- 'editUser',
		displayLabel <- 'Edit User',
		service <- thisModule.findWafService('Authentication'),
		omitCaption <- false,
		confirmLabel <- 'Save',
		displayFields <-
			let includedFeatures : Sequence(WAF!IncludedFeature)
				= thisModule.findWafService('Authentication').features
					->select(f | f.name <> 'password')
				in includedFeatures
					->select(f | f.oclIsKindOf(WAF!IncludedAttribute))
					->collect(f | thisModule.implicitUnitAttribute(f)),
		styleClass <- 'unit update_unit framed',
		headerClass <- 'unit_header',
		captionClass <- 'unit_caption',
		controlClass <- 'unit_control',
		footerClass <- 'unit_footer',
		errorClass <- 'error',
		layoutClass <- 'input_form'
	)
	do {
		waf;
	}
}
