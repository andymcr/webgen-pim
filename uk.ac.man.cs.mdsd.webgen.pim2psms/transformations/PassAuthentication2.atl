-- @atlcompiler emftvm 
--
-- @path Website=/uk.ac.man.cs.mdsd.webgen.model/model/website.ecore
-- @path ORM=/uk.ac.man.cs.mdsd.orm.model/model/orm.ecore
-- @path WAF=/uk.ac.man.cs.mdsd.waf.model/model/Waf.ecore

module PassAuthentication2;
create dummyWaf : WAF refining waf : WAF;

uses WebsiteHelpers;


abstract rule InterfaceField {
	from partialWaf : WAF!InterfaceField
	to waf : WAF!InterfaceField (
		mustMatch <-
			if not partialWaf.mustMatch.oclIsUndefined() then
				partialWaf.mustMatch
			else if partialWaf.name = 'confirmEmail' then
				partialWaf.displayedOn.displayFields->any(f | f.name = 'email')
			else if partialWaf.name = 'confirmPassword' then
				partialWaf.displayedOn.displayFields->any(f | f.name = 'password')
			else
				OclUndefined
			endif endif endif
	)
}

-- @extends InterfaceField
rule DataTypeField {
	from partialWaf : WAF!DataTypeField
	to waf : WAF!DataTypeField (
	)
}

-- @extends InterfaceField
rule DateField {
	from partialWaf : WAF!DateField
	to waf : WAF!DateField (
	)
}


rule LoginUnit {
	from partialWaf : WAF!LoginUnit
	to waf : WAF!LoginUnit (
		loginIdField <-
			if not partialWaf.loginIdField.oclIsUndefined() then
				partialWaf.loginIdField
			else
				partialWaf.displayFields->any(f | f.name = 'email')
			endif,
		passwordField <-
			if not partialWaf.passwordField.oclIsUndefined() then
				partialWaf.passwordField
			else
				partialWaf.displayFields->any(f | f.name = 'password')
			endif,
		rememberMeField <-
			if not partialWaf.rememberMeField.oclIsUndefined() then
				partialWaf.rememberMeField
			else
				partialWaf.displayFields->any(f | f.name = 'rememberMe')
			endif
	)
}

rule ForgottenPasswordUnit {
	from partialWaf : WAF!ForgottenPasswordUnit
	to waf : WAF!ForgottenPasswordUnit (
		loginIdField <-
			if not partialWaf.loginIdField.oclIsUndefined() then
				partialWaf.loginIdField
			else
				partialWaf.displayFields->any(f | f.name = 'email')
			endif
	)
}
