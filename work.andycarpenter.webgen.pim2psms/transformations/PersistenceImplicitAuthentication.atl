-- @atlcompiler emftvm 
--
-- @path Website=/work.andycarpenter.webgen.pims.model/model/website.ecore
-- @path ORM=/work.andycarpenter.psms.orm/model/orm.xcore

module PersistenceImplicitAuthentication;
create dummyOrm : ORM refining orm : ORM, website : Website;

uses PersistenceHelpers;
uses AuthenticationHelpers;


rule OrmModel {
	from partialOrm : ORM!OrmModel
	to orm : ORM!OrmModel (
		dataTypes <-
			let security : Website!Security = thisModule.findWebsiteSecurity()
			in partialOrm.dataTypes
				->union(
					if not security.isLocallyAuthenticated() then
						Sequence{}
					else
						let authentication : Website!LocalAuthenticationSystem
							= security.authentication
						in if authentication.authenticationModel.oclIsUndefined() then
								if thisModule.findOrmDataType('Boolean').oclIsUndefined() then
									Sequence{thisModule.implicitBooleanDataType(authentication)}
								else
									Sequence{}
								endif
							else
								Sequence{}
							endif
					endif)
				->union(
					if not security.isLocallyAuthenticated() then
						Sequence{}
					else
						let authentication : Website!LocalAuthenticationSystem
							= security.authentication
						in if authentication.authenticationModel.oclIsUndefined() then
								if thisModule.findOrmDataType('Email').oclIsUndefined() then
									Sequence{thisModule.implicitEmailDataType(authentication)}
								else
									Sequence{}
								endif
							else
								Sequence{}
							endif
					endif)
				->union(
					if not security.isLocallyAuthenticated() then
						Sequence{}
					else
						let authentication : Website!LocalAuthenticationSystem
							= security.authentication
						in if authentication.authenticationModel.oclIsUndefined() then
								if thisModule.findOrmDataType('String').oclIsUndefined() then
									Sequence{thisModule.implicitStringDataType(authentication)}
								else
									Sequence{}
								endif
							else
								Sequence{}
							endif
					endif),
		collectionTypes <-
			let security : Website!Security = thisModule.findWebsiteSecurity()
			in partialOrm.collectionTypes
				->union(
					if not security.isLocallyAuthenticated() then
						Sequence{}
					else
						let authentication : Website!LocalAuthenticationSystem
							= security.authentication
						in if authentication.authenticationModel.oclIsUndefined() then
								if thisModule.findOrmCollectionType('String').oclIsUndefined() then
									Sequence{thisModule.implicitStringCollectionType(authentication)}
								else
									Sequence{}
								endif
							else
								Sequence{}
							endif
					endif),
		entities <-
			let security : Website!Security = thisModule.findWebsiteSecurity()
			in partialOrm.entities
				->union(if not security.isLocallyAuthenticated() then
							Sequence{}
						else
							let authentication : Website!LocalAuthenticationSystem
								= security.authentication
								in if authentication.authenticationModel.oclIsUndefined() then
									Sequence{thisModule.implicitAuthenticationEntity(authentication)}
								else
									Sequence{}
								endif
						endif)
				->union(if not security.isLocallyAuthenticated() then
							Sequence{}
						else
							let authentication : Website!LocalAuthenticationSystem
								= security.authentication
								in if authentication.resetPasswordRequestModel.oclIsUndefined() then
										Sequence{thisModule.implicitResetPasswordRequestEntity(authentication)}
									else
										Sequence{}
									endif
						endif)
	)
}

unique lazy rule implicitBooleanDataType {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!DataType (
		name <- 'Boolean',
		displayLabel <- 'Boolean'
	)
	do {
		orm;
	}
}

unique lazy rule implicitEmailDataType {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!DataType (
		name <- 'Email',
		displayLabel <- 'Email'
	)
	do {
		orm;
	}
}

unique lazy rule implicitStringDataType {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!DataType (
		name <- 'String',
		displayLabel <- 'String'
	)
	do {
		orm;
	}
}

unique lazy rule implicitStringCollectionType {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!CollectionType (
		name <- 'String',
		elementType <- website.getDataType('String')
	)
	do {
		orm;
	}
}


unique lazy rule implicitAuthenticationEntity {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!Entity (
		name <- website.authenticationName,
		tableName <- 'authentications',
		autoKeyName <- 'id',
		autoKeyPersistentType <- 'integer',
		keys <- Sequence{email},
		displayLabel <- website.authenticationName.createLabel(),
		allFeatures <- Sequence{username, usernameCanonical, email, emailCanonical,
				password, enabled, locked, expired, expiresAt, confirmationToken,
				passwordRequestedAt, passwordChangedAt, credentialsExpired,
				credentialsExpireAt, roles, lastLogin, lastIp, userDetails}
			->union(
				if website.allowRememberMe and false then
					Sequence{thisModule.implicitAutoLoginOpposite(website)}
				else
					Sequence{}
				endif),
		labels <- Sequence{label},
		implementsUserInterface <- true
	),
	label : ORM!ModelLabel (
		name <- 'default',
		format <- '%s',
		features <- Sequence{labelFeature}
	),
	labelFeature : ORM!ModelLabelAttribute (
		attribute <- email
	),
	username : ORM!DataTypeAttribute (
		name <- 'username',
		displayLabel <- 'Username',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Required,
		"unique" <- true,
		columnName <- 'username'
	),
	usernameCanonical : ORM!DataTypeAttribute (
		name <- 'usernameCanonical',
		displayLabel <- 'Username (Canonical)',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Required,
		"unique" <- true,
		columnName <- 'username_canonical'
	),
	email : ORM!DataTypeAttribute (
		name <- 'email',
		displayLabel <- 'Email',
		attributeDataType <- website.getDataType('Email'),
		cardinality <- #Required,
		"unique" <- true,
		caseInsensitive <- true,
		columnName <- 'email'
	),
	emailCanonical : ORM!DataTypeAttribute (
		name <- 'emailCanonical',
		displayLabel <- 'Email (Canonical)',
		attributeDataType <- website.getDataType('Email'),
		cardinality <- #Required,
		"unique" <- true,
		caseInsensitive <- true,
		columnName <- 'email_canonical'
	),
	password : ORM!DataTypeAttribute (
		name <- 'password',
		displayLabel <- 'Password',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Required,
		"unique" <- false,
		obfuscateFormFields <- true,
		encrypt <- true,
		columnName <- 'password'
	),
	enabled : ORM!DataTypeAttribute (
		name <- 'enabled',
		displayLabel <- 'Enabled',
		attributeDataType <- website.getDataType('Boolean'),
		cardinality <- #Required,
		"unique" <- false,
		columnName <- 'enabled',
		attributeDefaultValue <- enabledDefault
	),
	enabledDefault : ORM!BooleanLiteral (
		value <- true
	),
	locked : ORM!DataTypeAttribute (
		name <- 'localed',
		displayLabel <- 'Locked',
		attributeDataType <- website.getDataType('Boolean'),
		cardinality <- #Required,
		"unique" <- false,
		columnName <- 'locked',
		attributeDefaultValue <- lockedDefault
	),
	lockedDefault : ORM!BooleanLiteral (
		value <- false
	),
	expired : ORM!DataTypeAttribute (
		name <- 'expired',
		displayLabel <- 'Expired',
		attributeDataType <- website.getDataType('Boolean'),
		cardinality <- #Required,
		"unique" <- false,
		columnName <- 'expired',
		attributeDefaultValue <- expiredDefault
	),
	expiredDefault : ORM!BooleanLiteral (
		value <- false
	),
	expiresAt : ORM!DateAttribute (
		name <- 'expiresAt',
		displayLabel <- 'Expires At',
		cardinality <- #Optional,
		attributeDefaultValue <- lastLoginDefault,
		columnName <- 'expires_at',
		details <- #DateAndTime
	),
	confirmationToken : ORM!DataTypeAttribute (
		name <- 'confirmationToken',
		displayLabel <- 'Confirmation Token',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Optional,
		"unique" <- false,
		columnName <- 'confirmation_token'
	),
	passwordRequestedAt : ORM!DateAttribute (
		name <- 'passwordRequestedAt',
		displayLabel <- 'Password Requested At',
		cardinality <- #Optional,
		columnName <- 'password_requested_at',
		details <- #DateAndTime
	),
	passwordChangedAt : ORM!DateAttribute (
		name <- 'passwordChangedAt',
		displayLabel <- 'Password Changed At',
		cardinality <- #Optional,
		columnName <- 'password_changed_at',
		details <- #DateAndTime
	),
	credentialsExpired : ORM!DataTypeAttribute (
		name <- 'credentialsExpired',
		displayLabel <- 'Credentials Expired',
		attributeDataType <- website.getDataType('Boolean'),
		cardinality <- #Optional,
		"unique" <- false,
		columnName <- 'credentials_expired'
	),
	credentialsExpireAt : ORM!DateAttribute (
		name <- 'credentialsExpireAt',
		displayLabel <- 'Credentials Expire At',
		cardinality <- #Optional,
		columnName <- 'credentials_expire_at',
		details <- #DateAndTime
	),
	roles : ORM!DataTypeAttribute (
		name <- 'roles',
		displayLabel <- 'Roles',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Many,
		singletonName <- 'role',
		columnName <- 'roles',
		collectionOrmAllowAdd <- true,
		collectionOrmAllowRemove <- true
	),
	lastLogin : ORM!DateAttribute (
		name <- 'lastLogin',
		displayLabel <- 'Last Login',
		cardinality <- #Required,
		columnName <- 'last_login',
		details <- #DateAndTime,
		attributeDefaultValue <- lastLoginDefault
	),
	lastLoginDefault : ORM!StringLiteral (
		value <- '0000-00-00 00:00:00'
	),
	lastIp : ORM!DataTypeAttribute (
		name <- 'lastIp',
		displayLabel <- 'lastIp',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Required,
		"unique" <- false,
		columnName <- 'last_ip',
		attributeDefaultValue <- lastIpDefault
	),
	lastIpDefault : ORM!StringLiteral (
		value <- '0.0.0.0'
	),
	userDetails : ORM!Association (
		name <- 'userDetails',
		displayLabel <- 'User details',
		cardinality <- #Required,
		associationOwningEnd <- true,
		visible <- true,
		associationRelationship <- #OneToOne,
		columnName <- website.user.name.createAssociationColumnName(),
		opposite <- thisModule.implicitUserAuthentication(website)
	)
	do {
		orm;
	}
}

lazy rule implicitUserAuthentication {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!Association (
		partOf <- thisModule.findOrmEntity(website.user.name),
		name <- website.authenticationName.lowercaseFirst(),
		displayLabel <- website.authenticationName,
		associationOwningEnd <- false,
		visible <- false,
		associationRelationship <- #OneToOne
	)
	do {
		orm;
	}
}

lazy rule implicitResetPasswordRequestEntity {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!Entity (
		name <- website.resetPasswordRequestName,
		tableName <- 'resetpasswordrequests',
		autoKeyName <- 'id',
		autoKeyPersistentType <- 'integer',
		displayLabel <- website.resetPasswordRequestName.createLabel(),
		allFeatures <- Sequence{selector, hashedToken, requestedAt, expiresAt, user},
		implementsResetPasswordInterface <- true
	),
	selector : ORM!DataTypeAttribute (
		name <- 'selector',
		displayLabel <- 'Selector',
		attributeDataType <-
			if website.authenticationKey = #Email then
				website.getDataType('Email')
			else
				website.getDataType('String')
			endif,
		cardinality <- #Required,
		"unique" <- false,
		columnName <- 'selector'
	),
	hashedToken : ORM!DataTypeAttribute (
		name <- 'ipAddress',
		displayLabel <- 'IP Address',
		attributeDataType <- website.getDataType('String'),
		cardinality <- #Required,
		"unique" <- false,
		columnName <- 'hashed_token'
	),
	requestedAt : ORM!DateAttribute (
		name <- 'requestedAt',
		displayLabel <- 'Requested At',
		cardinality <- #Required,
		columnName <- 'requested_at',
		details <- #DateAndTime
	),
	expiresAt : ORM!DateAttribute (
		name <- 'expiresAt',
		displayLabel <- 'Expires At',
		cardinality <- #Required,
		columnName <- 'expires_at',
		details <- #DateAndTime
	),
	user : ORM!Association (
		name <- 'user',
		displayLabel <- 'User',
		cardinality <- #Required,
		associationOwningEnd <- true,
		visible <- true,
		associationRelationship <- #ManyToOne,
		columnName <-
			if website.authenticationModel.oclIsUndefined() then
				website.authenticationName.createAssociationColumnName()
			else
				website.authenticationModel.name.createAssociationColumnName()
			endif,
		opposite <- thisModule.implicitUserAuthentication2(website),
		serializationMaxDepth <- 0
	)
	do {
		orm;
	}
}

lazy rule implicitUserAuthentication2 {
	from website: Website!LocalAuthenticationSystem
	to orm : ORM!Association (
		partOf <-
			if website.authenticationModel.oclIsUndefined() then
				thisModule.implicitAuthenticationEntity(website)
			else
				thisModule.findOrmEntity(website.authenticationModel.name)
			endif,
		name <- website.resetPasswordRequestName.lowercaseFirst(),
		displayLabel <- website.resetPasswordRequestName,
		associationOwningEnd <- false,
		visible <- false,
		associationRelationship <- #OneToMany
	)
	do {
		orm;
	}
}