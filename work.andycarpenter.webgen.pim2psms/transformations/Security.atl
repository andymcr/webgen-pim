-- @atlcompiler emftvm 
--
-- @path Website=/work.andycarpenter.webgen.pims.model/model/website.ecore
-- @path Security=/work.andycarpenter.metamodel.security/model/security.xcore

module Security;
create security : Security from website : Website, orm : ORM;

uses PersistenceHelpers;


rule SecurityModel {
	from website : Website!Security
	to security : Security!SecurityModel (
		authentication <- website.authentication
	)
}


abstract rule Authentication {
	from website : Website!Authentication
	to security : Security!Authentication (
		userModel <-thisModule.findOrmEntity(website.userModel.name)
	)
}

-- @extends Authentication
rule LocalAuthenticationSystem {
	from website : Website!LocalAuthenticationSystem
	to security : Security!LocalAuthenticationSystem (
		authenticationModel <-
			if website.authenticationModel.oclIsUndefined() then
				thisModule.findOrmEntity(website.authenticationName)
			else
				thisModule.findOrmEntity(website.authenticationModel.name)
			endif,
		userKey <-
			if not website.userKey.oclIsUndefined() then
				website.userKey.findOrmAttribute()
			else if website.authenticationModel.oclIsUndefined() then
				thisModule.findOrmEntity(website.authenticationName).getOrmFeature('username')
			else
				thisModule.findOrmEntity(website.authenticationModel.name).getOrmFeature('username')
			endif endif,
		authenticationKey <-
			if not website.authenticationKey.oclIsUndefined() then
				website.authenticationKey.findOrmAttribute()
			else
				thisModule.findOrmEntity(website.userModel.name).getOrmFeature('username')
			endif,
		identifierFeature <-
			if not website.identifierFeature.oclIsUndefined() then
				website.identifierFeature.findOrmAttribute()
			else if website.authenticationModel.oclIsUndefined() then
				thisModule.findOrmEntity(website.authenticationName).getOrmFeature('email')
			else
				thisModule.findOrmEntity(website.authenticationModel.name).getOrmFeature('email')
			endif endif,
		passwordFeature <-
			if not website.passwordFeature.oclIsUndefined() then
				website.passwordFeature.findOrmAttribute()
			else if website.authenticationModel.oclIsUndefined() then
				thisModule.findOrmEntity(website.authenticationName).getOrmFeature('password')
			else
				thisModule.findOrmEntity(website.authenticationModel.name).getOrmFeature('password')
			endif endif,
		resetPasswordRequestModel <-
			if website.resetPasswordRequestModel.oclIsUndefined() then
				thisModule.findOrmEntity(website.resetPasswordRequestName)
			else
				thisModule.findOrmEntity(website.resetPasswordRequestModel.name)
			endif,
		captcha <- website.useCaptcha,
		allowRememberMe <- website.allowRememberMe,
		allowSelfRegistration <- website.allowSelfRegistration,
		useEmailActivation <- website.useEmailActivation,
		sendWelcomeEmail <- website.sendWelcomeEmail,
		sectionClass <- thisModule.findWebsiteUI().defaultFormStyles.sectionClass,
		captionClass <- thisModule.findWebsiteUI().defaultFormStyles.captionClass,
		contentClass <- thisModule.findWebsiteUI().defaultFormStyles.contentClass
	)
}

-- @extends Authentication
rule CasAuthentication {
	from website : Website!CasAuthentication
	to security : Security!CasAuthentication (
		loginLabel <- website.loginLabel,
		logoutLabel <- website.logoutLabel
	)
}