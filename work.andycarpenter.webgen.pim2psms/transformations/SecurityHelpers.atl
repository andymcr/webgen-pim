-- @atlcompiler emftvm

library SecurityHelpers;

uses WebsiteHelpers;


helper def: findWebsiteSecurity() : Website!Security
	= thisModule.findWebGenModel().security;

helper def: findWafSecurity() : Security!SecurityModel
	= Security!SecurityModel.allInstancesFrom('security')->first();

helper context Website!Security def: isLocallyAuthenticated() : Boolean
	= if self.authentication.oclIsUndefined() then
			false
		else
			self.authentication.oclIsTypeOf(Website!LocalAuthenticationSystem)
		endif;

helper context Website!Security def: hasImplicitAuthenticationPage() : Boolean
	= if not self.isLocallyAuthenticated() then	
			false
		else
			self.authentication.authenticationModel.oclIsUndefined()
		endif;

helper context Website!Security def: hasImplicitSecurityPage() : Boolean
	= if not self.isLocallyAuthenticated() then	
			false
		else
			let authentication : Website!LocalAuthenticationSystem
				= self.authentication
				in
					authentication.registrationUnit.oclIsUndefined()
						and authentication.allowSelfRegistration
					or authentication.loginUnit.oclIsUndefined()
					or authentication.logoutUnit.oclIsUndefined()
					or authentication.forgottonPasswordUnit.oclIsUndefined()
		endif;


helper context Website!Authentication def: getDataType(name : String) : ORM!DataType
	= let dataType : ORM!DataType = thisModule.findOrmDataType(name)
		in if not dataType.oclIsUndefined() then
			dataType
		else
			let webGenModel : Website!WebGenModel = thisModule.findWebGenModel()
			in if name = 'Boolean' then
					thisModule.implicitBooleanDataType(webGenModel)
				else if name = 'Email' then
					thisModule.implicitEmailDataType(webGenModel)
				else if name = 'String' then
					thisModule.implicitStringDataType(webGenModel)
				else
					OclUndefined
				endif endif endif
		endif;


helper def: findWafRole(name : String) : Security!Role
	= thisModule.findWafSecurity().roles->any(r | r.name = name);

helper context Website!Role def: findWafRole() : Security!Role
	= thisModule.findWafRole(self.name);