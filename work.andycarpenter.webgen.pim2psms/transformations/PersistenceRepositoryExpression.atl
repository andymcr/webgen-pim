-- @atlcompiler emftvm 
--
-- @path Website=/uk.ac.man.cs.mdsd.webgen.model/model/website.ecore
-- @path ORM=/uk.ac.man.cs.mdsd.orm.model/model/orm.ecore

module PersistenceRepositoryExpression;
create orm : ORM from website : Website;

helper context Website!Expression def: isPresistenceRepositoryExpression() : Boolean
	= if self.rootContainer.oclIsUndefined() then
			false
		else if self.rootContainer.oclIsKindOf(Website!Selection) then
			true
		else if self.rootContainer.oclIsKindOf(Website!Order) then
			true
		else if self.rootContainer.oclIsKindOf(Website!Filter) then
			true
		else
			false
		endif endif endif endif;


rule NullLiteral {
	from website : Website!NullLiteral (website.isPresistenceRepositoryExpression())
	to orm : ORM!NullLiteral (
	)
}

rule BooleanLiteral {
	from website : Website!BooleanLiteral (website.isPresistenceRepositoryExpression())
	to orm : ORM!BooleanLiteral (
		value <- website.value
	)
}

rule IntegerLiteral {
	from website : Website!IntegerLiteral (website.isPresistenceRepositoryExpression())
	to orm : ORM!IntegerLiteral (
		value <- website.value
	)
}

rule StringLiteral {
	from website : Website!StringLiteral (website.isPresistenceRepositoryExpression())
	to orm : ORM!StringLiteral (
		value <- website.value
	)
}

rule TimeLiteral {
	from website : Website!TimeLiteral (website.isPresistenceRepositoryExpression())
	to orm : ORM!TimeLiteral (
		value <- website.value
	)
}


rule CurrentTime {
	from website : Website!CurrentTime (website.isPresistenceRepositoryExpression())
	to orm : ORM!CurrentTime (
	)
}

rule CurrentUser {
	from website : Website!CurrentUser (website.isPresistenceRepositoryExpression())
	to orm : ORM!CurrentUser (
	)
}


rule FunctionCall {
	from website : Website!FunctionCall (website.isPresistenceRepositoryExpression())
	to orm : ORM!FunctionCall (
		name <- website.name,
		actuals <- website.actuals
	)
}


abstract rule Predicate {
	from website : Website!Predicate
	to orm : ORM!Predicate (
		negated <- website.negated
	)
}

-- @extends Predicate
rule PredicateBooleanVariable {
	from website : Website!PredicateBooleanVariable (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateBooleanVariable (
		feature <- website.feature
	)
}

-- @extends Predicate
rule PredicateBooleanOperator {
	from website : Website!PredicateBooleanOperator (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateBooleanOperator (
		predicates <- website.expressions,
		operator <- website.operator
	)
}

-- @extends Predicate
rule PredicateEqualityOperator {
	from website : Website!PredicateEqualityOperator (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateEqualityOperator (
		left <- website.left,
		right <- website.right
	)
}

-- @extends Predicate
rule PredicateComparisonOperator {
	from website : Website!PredicateComparisonOperator (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateComparisonOperator (
		left <- website.left,
		operator <- website.operator,
		right <- website.right
	)
}

-- @extends Predicate
rule PredicateLikeOperator {
	from website : Website!PredicateLikeOperator (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateLikeOperator (
		left <- website.left,
		right <- website.right
	)
}

-- @extends Predicate
rule PredicateIsNullOperator {
	from website : Website!PredicateIsNull (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateIsNull (
		feature <- website.feature
	)
}

-- @extends Predicate
rule PredicateIsEmptyOperator {
	from website : Website!PredicateIsEmpty (website.isPresistenceRepositoryExpression())
	to orm : ORM!PredicateIsEmpty (
		feature <- website.feature
	)
}