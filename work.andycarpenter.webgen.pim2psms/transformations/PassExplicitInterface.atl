-- @atlcompiler emftvm 
--
-- @path Website=/work.andycarpenter.webgen.pims.model/model/website.ecore
-- @path ORM=/work.andycarpenter.psms.orm/model/orm.xecore
-- @path WAF=/work.andycarpenter.psms.waf/model/Waf.xecore

module PassExplicitInterface;
create waf : WAF from website : Website, orm : ORM, service : Service, api : API;

uses WebsiteHelpers;
uses InterfaceExpression;


helper context String def: createName() : String
	= if self.trim().replaceAll('[A-Z]', '') = '' then
			self.trim()
		else
			self.trim().lowercaseFirst().replaceAll('([A-Z])', '_$1')
		endif;

helper context Website!Attribute def: attributeLabel() : String
	= if not self.displayLabel.oclIsUndefined() and self.displayLabel <> '' then
			self.displayLabel
		else
			self.name.createLabel()
		endif;

helper context Website!Association def: associationLabel() : String
	= if not self.displayLabel.oclIsUndefined() and self.displayLabel <> '' then
			self.displayLabel
		else
			self.name.createLabel()
		endif;

helper context Website!Association def: associationTargetLabel() : String
	= if not self.targetDisplayLabel.oclIsUndefined() and self.targetDisplayLabel <> '' then
			self.targetDisplayLabel
		else
			self.targetFeatureName().createLabel()
		endif;

helper context Website!UnitElement def: attributeLabel() : String
	= if not self.displayLabel.oclIsUndefined() and  self.displayLabel <> '' then
			self.displayLabel
		else
			self.attribute.attributeLabel()
		endif;

helper context Website!UnitAssociation def: associationLabel() : String
	= if not self.displayLabel.oclIsUndefined() and  self.displayLabel <> ''
				or self.association.oclIsUndefined() then
			self.displayLabel
		else if self.isSourceAssociation then
			self.association.associationLabel()
		else
			self.association.associationTargetLabel()
		endif endif;


helper context Website!UnitField def: title() : String
	= if self.title.trim() <> '' then
			self.title.trim()
		else
			if self.oclIsTypeOf(Website!UnitElement) then
				if not self.attribute.title.oclIsUndefined() and self.attribute.title <> '' then
					self.attribute.title
				else
					self.name.createLabel()
				endif
			else if self.oclIsTypeOf(Website!UnitAssociation) then
				if not self.association.title.oclIsUndefined() and self.association.title <> '' then
					self.association.title
				else
					self.name.createLabel()
				endif
			else
				self.name.createLabel()
			endif endif
		endif;

helper context Website!Attribute def: interfaceType() : String
	= if self.oclIsTypeOf(Website!DataTypeAttribute) then
			if not self.interfaceType.oclIsUndefined() and self.interfaceType <> '' then
				self.interfaceType
			else
				if self.oclIsTypeOf(Website!DataTypeAttribute) then
					self.dataType.interfaceType
				else
					self.name.lowercaseFirst()
				endif
			endif
		else if self.oclIsTypeOf(Website!DateAttribute) then
			if self.details = #DateAndTime then
				'datetime'
			else if self.details = #DateOnly then
				'date'
			else
				'time'
			endif endif
		else
			OclUndefined
		endif endif;

helper context Website!DataTypeField def: interfaceType() : String
	= if not self.interfaceType.oclIsUndefined() and self.interfaceType <> '' then
			self.interfaceType
		else
			self.dataType.interfaceType
		endif;

helper context Website!UnitElement def: placeholder() : String
	= if not self.placeholder.oclIsUndefined() and self.placeholder <> '' then
			self.placeholder
		else if not self.attribute.placeholder.oclIsUndefined() and self.attribute.placeholder <> '' then
			self.attribute.placeholder
		else
			if self.attribute.oclIsTypeOf(Website!DataTypeAttribute) then
				if not self.attribute.dataType.placeholder.oclIsUndefined() then
					self.attribute.dataType.placeholder
				else
					self.name.createLabel()
				endif
			else
				self.name.createLabel()
			endif
		endif endif;

helper context Website!InterfaceField def: placeholder() : String
	= if not self.placeholder.oclIsUndefined() and self.placeholder <> '' then
			self.placeholder
		else
			if self.oclIsTypeOf(Website!DataTypeField) then
				if not self.dataType.placeholder.oclIsUndefined() and self.dataType.placeholder <> '' then
					self.dataType.placeholder
				else
					self.name.createLabel()
				endif
			else
				self.name.createLabel()
			endif
		endif;

helper context Website!InterfaceField def: validationPattern() : String
	= if not self.validationPattern.oclIsUndefined() and self.validationPattern <> '' then
			self.validationPattern
		else
			if self.oclIsTypeOf(Website!DataTypeField) then
				self.dataType.validationPattern
			else
				OclUndefined
			endif
		endif;

helper context Website!UnitFeature def: defaultDisplayValue() : String
	= if not self.defaultDisplayValue.oclIsUndefined() and self.defaultDisplayValue <> '' then
			self.defaultDisplayValue
		else
			if self.oclIsTypeOf(Website!UnitElement) then
				if not self.attribute.defaultDisplayValue.oclIsUndefined() and self.attribute.defaultDisplayValue <> '' then
					self.attribute.defaultDisplayValue
				else
					self.defaultDisplayValue
				endif
			else
				if not self.association.defaultDisplayValue.oclIsUndefined() and self.association.defaultDisplayValue <> '' then
					self.association.defaultDisplayValue
				else
					self.defaultDisplayValue
				endif
			endif
		endif;

helper context Website!UnitFeature def: emptyDisplayValue() : String
	= if not self.emptyDisplayValue.oclIsUndefined() and self.emptyDisplayValue <> '' then
			self.emptyDisplayValue
		else
			if self.oclIsTypeOf(Website!UnitElement) then
				if not self.attribute.emptyDisplayValue.oclIsUndefined() and self.attribute.emptyDisplayValue <> '' then
					self.attribute.emptyDisplayValue
				else
					self.emptyDisplayValue
				endif
			else
				if not self.association.emptyDisplayValue.oclIsUndefined() and self.association.emptyDisplayValue <> '' then
					self.association.emptyDisplayValue
				else
					self.emptyDisplayValue
				endif
			endif
		endif;

helper context Website!Association def: findOrmEntity(entity : Website!Entity) : ORM!Entity
	= if entity.features->includes(self) then
			thisModule.findOrmEntity(entity.name)
		else if self.oclIsKindOf(Website!Association) then
			thisModule.findOrmEntity(self.targetEntity.name)
		else -- EncapsulatedAssociation
			thisModule.findOrmEntity(self.partOf.name) -- TODO fix
		endif endif;

helper context Website!Expression def: findContainingSelection() : Website!Selection
	= if self.refImmediateComposite().oclIsUndefined() then
			OclUndefined
		else if self.refImmediateComposite().oclIsTypeOf(Website!Selection) then
			self.refImmediateComposite()
		else if self.refImmediateComposite().oclIsKindOf(Website!Expression) then
			self.refImmediateComposite().findContainingSelection()
		else
			OclUndefined
		endif endif endif ;

helper context Website!Label def: findOrmLabel() : ORM!Label
	= if self.oclIsKindOf(Website!Attribute) then
		self.findOrmEntity().getOrmFeature(self.name)
	else if self.oclIsTypeOf(Website!ModelLabel) then
		thisModule.findOrmEntity(self.labelFor.name).getOrmLabel(self.name)
	else
		self.title
	endif endif;

helper context Website!Entity def: container() : Website!Entity
	= let containingAssociation : Sequence(Website!AssociationWithContainment)
		= self.allAssociations
				->select(a | a.oclIsTypeOf(Website!AssociationWithContainment) and a.partOf <> self)
			in if containingAssociation->isEmpty() then
					OclUndefined
				else
					containingAssociation->first().partOf
				endif;


rule WafModel {
	from website : Website!WebGenModel, orm : ORM!OrmModel, service : Service!ServiceModel, api : API!API
	to waf : WAF!WafModel (
		persistence <- orm,
		business <- service,
		imageManipulations <-
			if not website.images.oclIsUndefined() then
				website.images.manipulations
			else
				Sequence{}
			endif,
		api <- api,
		menus <- website.webUI.menus,
		pages <- website.webUI.pages,
		siteName <- '?',
		siteTitle <- website.websiteProperties.siteTitle,
		webmasterEmail <- website.websiteProperties.webmasterEmail,
		copyrightText <-
			if not website.websiteProperties.copyrightText.oclIsUndefined() and website.websiteProperties.copyrightText <> '' then
				website.websiteProperties.copyrightText
			else
				'&copy; '.concat(website.websiteProperties.siteTitle).concat(' {{ \'now\' | date(\'Y\') }}')
			endif,
		metaDescription <- website.websiteProperties.metaDescription,
		logoImage <- website.websiteProperties.logoImage,
		logoFilters <- website.websiteProperties.logoFilters,
		frameworkTechnology <- website.frameworkTechnology,
		useDbSessionStorage <- website.websiteProperties.useDbSessionStorage,
		inputTechnology <- thisModule.findWebsiteUI().inputTechnology,
		ajaxTechnology <- thisModule.findWebsiteUI().ajaxTechnology,
		captchaSiteKey <- thisModule.findWebsiteSecurity().captchaSiteKey,
		captchaSecretKey <- thisModule.findWebsiteSecurity().captchaSecretKey,
		textEditorURL <-
			if website.websiteProperties.textEditorURL.oclIsUndefined() then
				OclUndefined
			else
				website.websiteProperties.textEditorURL.cleanString()
			endif,
		topNavigationId <- website.websiteProperties.topNavigationId,
		siteTemplate <- website.websiteProperties.siteTemplate,
		staticUnitsEditable <- website.websiteProperties.staticUnitsEditable
	)
}


abstract rule NamedElement {
	from website : Website!NamedElement
	to waf : WAF!NamedElement (
		name <- website.name
	)
}

-- @extends NamedElement
abstract rule NamedDisplayElement {
	from website : Website!NamedDisplayElement
	to waf : WAF!NamedDisplayElement (
		displayLabel <-
			if not website.displayLabel.oclIsUndefined() and website.displayLabel <> '' then
				website.displayLabel
			else
				website.name.createLabel()
			endif
	)
}


-- @extends NamedElement
rule ImageManipulation {
	from website : Website!ImageManipulation
	to waf : WAF!ImageManipulation (
		jpegQuality <- website.jpegQuality,
		filters <- website.filters
	)
}

abstract rule ImageFilter {
	from website : Website!ImageFilter
	to waf : WAF!ImageFilter (
	)
}

-- @extends ImageFilter
rule BackgroundFilter {
	from website : Website!BackgroundFilter
	to waf : WAF!BackgroundFilter (
		color <- website.color,
		width <- website.width,
		height <- website.height,
		position <- website.position
	)
}
-- @extends ImageFilter
rule ThumbnailFilter {
	from website : Website!ThumbnailFilter
	to waf : WAF!ThumbnailFilter (
		width <- website.width,
		height <- website.height,
		mode <- website.mode
	)
}



-- @extends NamedDisplayElement
rule Menu {
	from website : Website!Menu
	to waf : WAF!Menu (
		entries <- website.entries,
		omitCaption <-
			if not website.omitCaption.oclIsUndefined() then
				website.omitCaption
			else
				false
			endif,
		captionClass <- website.captionClass.replaceAll(',', ' '),
		styleClass <- website.styleClass.replaceAll(',', ' '),
		layoutClass <- website.layoutClass.replaceAll(',', ' ')
	)
}

abstract rule MenuEntry {
	from website : Website!MenuEntry
	to waf : WAF!MenuEntry (
	)
}

-- @extends MenuEntry, Menu
rule SubmenuEntry {
	from website : Website!SubmenuEntry
	to waf : WAF!SubmenuEntry (
		requiresRole <- website.requiresRole.trim()
	)
}

-- @extends MenuEntry, NamedDisplayElement
rule ActionMenuEntry {
	from website : Website!ActionMenuEntry
	to waf : WAF!ActionMenuEntry (
		action <- website.destination,
		"query" <- website."query"
	)
}

-- @extends MenuEntry, NamedDisplayElement
rule EditStaticTextMenuEntry {
	from website : Website!EditStaticTextMenuEntry
	to waf : WAF!EditStaticTextMenuEntry (
	)
}

-- @extends NamedDisplayElement, UnitContainer
rule Page {
	from website : Website!Page
	to waf : WAF!Page (
		uriElement <- website.uriElement.trim(),
		parentPage <-
			if website.parentPage.oclIsUndefined() then
				OclUndefined
			else
				website.parentPage.targetPage
			endif,
		styleClass <-
			if website.styleClass.oclIsUndefined() then
				OclUndefined
			else
				website.styleClass.replaceAll(',', ' ')
			endif
	)
}


rule Query {
	from website : Website!Query
	to waf : WAF!Query (
		filter <- website.filter,
		parameters <- website.parameters
	)
}

rule QueryParameter {
	from website : Website!QueryParameter
	to waf : WAF!QueryParameter (
		formal <- website.formal,
		value <- website.value
	)
}

abstract rule UnitContainer {
	from website : Website!UnitContainer
	to waf : WAF!UnitContainer (
		units <- website.units
	)
}


-- @extends NamedDisplayElement
abstract rule ContentUnit {
	from website : Website!ContentUnit
	to waf : WAF!ContentUnit (
		uriElement <-
			if website.uriElement.trim() <> '' then
				website.uriElement.trim()
			else if website.createDefaultUriElement then
				website.name.createName()
			else
				''
			endif endif,
		routeActuals <- website.routeActuals,
		requiresRole <- website.requiresRole.trim(),
		purposeSummary <- website.purposeSummary,
		omitCaption <-
			if not website.omitCaption.oclIsUndefined() then
				website.omitCaption
			else
				false
			endif,
		alternative <- website.alternative,
		captionClass <- website.captionClass.replaceAll(',', ' '),
		styleClass <- website.styleClass.replaceAll(',', ' '),
		contentClass <- website.contentClass.replaceAll(',', ' ')
	)
}

rule RouteActual {
	from website : Website!RouteActual
	to waf : WAF!RouteActual (
		routeParameter <- website.routeParameter.findOrmAttribute(),
		value <- website.value
	)
}

-- @extends ContentUnit
rule StaticUnit {
	from website : Website!StaticUnit
	to waf : WAF!StaticUnit (
		content <- website.content
	)
}


-- @extends ContentUnit
abstract rule DynamicUnit {
	from website : Website!DynamicUnit
	to waf : WAF!DynamicUnit (
		displayFields <- website.displayFields,
		servicesUsed <- website.findServicesUsed(),
		hideWhen <- website.hideWhen,
		messageWhenHidden <- website.messageWhenHidden.trim(),
		supportActions <- website.supportActions,
		header <- website.header,
		footer <- website.footer,
		headerClass <- website.headerClass,
		controlClass <- website.controlClass,
		footerClass <- website.footerClass,
		errorClass <- website.errorClass
	)
}

abstract rule UnitField {
	from website : Website!UnitField
	to waf : WAF!UnitField (
		requiresRole <- website.requiresRole.trim(),
		hideWhen <- website.hideWhen,
		title <- website.title(),
		escapeHTML <- website.escapeHTML,
		collectionDisplayOption <- website.collectionDisplayOption,
		maximumDisplaySize <- website.maximumDisplaySize,
		truncate <- website.truncate,
		dateFormat <- website.dateFormat(),
		disableInput <- website.disableInput
	)
}

-- @extends UnitField, InlineActionContainer
abstract rule UnitFeature {
	from website : Website!UnitFeature
	to waf : WAF!UnitFeature (
		forcedValue <- website.forcedValue,
		collectionUiAllowAdd <- website.collectionUiAllowAdd,
		collectionUiAllowRemove <- website.collectionUiAllowRemove,
		defaultDisplayValue <- website.defaultDisplayValue(),
		displayDefaultWhen <- website.displayDefaultWhen,
		emptyDisplayValue <- website.emptyDisplayValue(),
		autofocus <- website.autofocus,
		footer <- website.footer
	)
}

-- @extends UnitFeature
rule UnitAttribute {
	from website : Website!UnitElement
	to waf : WAF!UnitAttribute (
		attribute <- website.attribute.findOrmAttribute(),
		displayLabel <- website.attributeLabel(),
		defaultValue <- website.defaultValue,
		obfuscateFormFields <- website.obfuscateFormFields,
		placeholder <- website.placeholder(),
		validationPattern <- website.validationPattern,
		headerClass <-
			if not website.headerClass.oclIsUndefined() and website.headerClass <> '' then
				website.headerClass
			else
				if not website.attribute.oclIsUndefined() then
					website.attribute.headerClass
				else
					OclUndefined
				endif
			endif,
		inputClass <-
			if not website.inputClass.oclIsUndefined() and website.inputClass <> '' then
				website.inputClass.replaceAll(',', ' ')
			else
				if not website.attribute.oclIsUndefined() then
					if not website.attribute.inputClass.oclIsUndefined() then
						website.attribute.inputClass.replaceAll(',', ' ')
					else
						OclUndefined
					endif
				else
					OclUndefined
				endif
			endif,
		displayClass <-
			if not website.displayClass.oclIsUndefined() and website.displayClass <> '' then
				website.displayClass
			else
				if not website.attribute.oclIsUndefined() then
					website.attribute.displayClass
				else
					OclUndefined
				endif
			endif,
		footerClass <-
			if not website.footerClass.oclIsUndefined() and website.footerClass <> '' then
				website.footerClass
			else
				if not website.attribute.oclIsUndefined() then
					website.attribute.footerClass
				else
					OclUndefined
				endif
			endif
	)
}

-- @extends UnitFeature, UnitContainer, AssociationReference
rule UnitAssociation {
	from website : Website!UnitAssociation
	to waf : WAF!UnitAssociation (
		displayLabel <- website.associationLabel(),
		options <-
			if website.options.oclIsUndefined() then
				website.options
			else
				website.options.findServiceSelection()
			endif,
		filteredOptions <-
			if website.filteredOptions.oclIsUndefined() then
				website.filteredOptions
			else
				website.filteredOptions.findFilter()
			endif,
		headerClass <-
			if not website.headerClass.oclIsUndefined() and website.headerClass <> '' then
				website.headerClass
			else
				if not website.association.oclIsUndefined() then
					website.association.headerClass
				else
					OclUndefined
				endif
			endif,
		inputClass <-
			if not website.inputClass.oclIsUndefined() and website.inputClass <> '' then
				website.inputClass.replaceAll(',', ' ')
			else
				if not website.association.oclIsUndefined() then
					website.association.inputClass.replaceAll(',', ' ')
				else
					OclUndefined
				endif
			endif,
		displayClass <-
			if not website.displayClass.oclIsUndefined() and website.displayClass <> '' then
				website.displayClass
			else
				if not website.association.oclIsUndefined() then
					website.association.displayClass
				else
					OclUndefined
				endif
			endif,
		footerClass <-
			if not website.footerClass.oclIsUndefined() and website.footerClass <> '' then
				website.footerClass
			else
				if not website.association.oclIsUndefined() then
					website.association.footerClass
				else
					OclUndefined
				endif
			endif
	)
}

abstract rule AssociationReference {
	from website : Website!AssociationReference
	to waf : WAF!AssociationReference (
		association <- website.findOrmAssociation(),
		valueDisplay <-
			if website.valueDisplay.oclIsUndefined() then
				OclUndefined
			else
				website.valueDisplay.findOrmLabel()
			endif,
		childFeature <- website.childFeature
	)
}

abstract rule ChildPath {
	from website : Website!ChildPath
	to waf : WAF!ChildPath (
	)
}

-- @extends ChildPath
rule ChildPathAttribute {
	from website : Website!ChildPathAttribute
	to waf : WAF!ChildPathAttribute (
		attribute <- website.attribute.findOrmAttribute()
	)
}

-- @extends ChildPath, AssociationReference
rule ChildPathAssociation {
	from website : Website!ChildPathAssociation
	to waf : WAF!ChildPathAssociation (
	)
}

-- @extends Expression
abstract rule FeaturePath {
	from website : Website!FeaturePath
	to waf : WAF!FeaturePath (
	)
}

-- @extends FeaturePath
rule FeaturePathAttribute {
	from website : Website!FeaturePathAttribute
	to waf : WAF!FeaturePathAttribute (
		attribute <- website.attribute.findOrmAttribute()
	)
}

-- @extends FeaturePath, AssociationReference
rule FeaturePathAssociation {
	from website : Website!FeaturePathAssociation
	to waf : WAF!FeaturePathAssociation (
	)
}

-- @extends FeaturePath
rule FeaturePathLabel {
	from website : Website!FeaturePathLabel
	to waf : WAF!FeaturePathLabel (
		label <- website.label.findOrmLabel()
	)
}

-- @extends UnitField
rule UnitLabel {
	from website : Website!UnitLabel
	to waf : WAF!UnitLabel (
		label <- website.label.findOrmLabel()
	)
}

-- @extends UnitField, NamedElement
abstract rule InterfaceField {
	from website : Website!InterfaceField
	to waf : WAF!InterfaceField (
		displayLabel <-
			if not website.displayLabel.oclIsUndefined() and website.displayLabel <> '' then
				website.displayLabel
			else
				website.name.createLabel()
			endif,
		required <- website.required,
		defaultValue <- website.defaultValue,
		mustMatch <- website.mustMatch,
		placeholder <- website.placeholder(),
		validationPattern <- website.validationPattern(),
		inputClass <- website.inputClass
	)
}

-- @extends InterfaceField
rule DataTypeField {
	from website : Website!DataTypeField
	to waf : WAF!DataTypeField (
		dataType <- thisModule.findOrmDataType(website.dataType.name),
--		caseInsensitive <- website.caseInsensitive,
		obfuscateFormFields <- website.obfuscateFormFields,
		encrypt <- website.encrypt
	)
}

-- @extends InterfaceField
rule DateField {
	from website : Website!DateField
	to waf : WAF!DateField (
		details <- website.details,
		format <- website.format
	)
}

-- @extends InterfaceField
rule CaptchaField {
	from website : Website!CaptchaField
	to waf : WAF!CaptchaField (
	)
}

-- @extends NamedDisplayElement
rule UnitSupportAction {
	from website : Website!UnitSupportAction
	to waf : WAF!UnitSupportAction (
		disable <- website.disable,
		confirmMessage <- website.confirmMessage
	)
}

abstract rule SelectableUnit {
	from website : Website!SelectableUnit
	to waf : WAF!SelectableUnit (
	)
}


-- @extends DynamicUnit
abstract rule SingletonUnit {
	from website : Website!SingletonUnit
	to waf : WAF!SingletonUnit (
		title <-
			if website.title.oclIsUndefined() then
				website.title
			else
				website.title.findOrmLabel()
			endif,
		contentType <- thisModule.findOrmEntity(website.contentType.name)
	)
}


-- @extends DynamicUnit, SelectableUnit
abstract rule CollectionUnit {
	from website : Website!CollectionUnit
	to waf : WAF!CollectionUnit (
		unitTitle <-
			if website.unitTitle.oclIsUndefined() then
				website.unitTitle
			else
				website.unitTitle.findOrmLabel()
			endif,
		elementTitle <- website.elementTitle,
		truncateElementTitle <- website.truncateElementTitle,
		contentType <- website.contentType
			->collect(eov | thisModule.findOrmEntity(eov.name)),
		selection <-
			if website.selection.oclIsUndefined() then
				website.selection
			else
				website.selection.findServiceSelection()
			endif,
		omitContainerLoad <- website.omitContainerLoad,
		containerType <- website.findContainerType(),
		containerService <- website.findContainerService(),
		findContainerSelection <-
			if website.findContainerSelection.oclIsUndefined() then
				website.findContainerSelection
			else
				website.findContainerSelection.findServiceSelection()
			endif,
		findElementSelection <-
			if website.findElementSelection.oclIsUndefined() then
				website.findElementSelection
			else
				website.findElementSelection.findServiceSelection()
			endif,
		filter <-
			if website.filter.oclIsUndefined() then
				website.filter
			else
				website.filter.findFilter()
			endif,
		supportedFilters <-	website.supportedFilters->collect(f | f.findFilter()),
		badges <- website.badges,
		defaultPaginationSize <- website.defaultPaginationSize,
		maximumPaginationSize <- website.maximumPaginationSize,
		paginationControlPlacement <- website.paginationControlPlacement,
		emptyMessage <- website.emptyMessage,
		nextNpages <- website.nextNpages,
		previousNpages <- website.previousNpages,
		nextPageLabel <- website.nextPageLabel,
		nextPageIconName <- website.nextPageIconName,
		previousPageLabel <- website.previousPageLabel,
		previousPageIconName <- website.previousPageIconName,
		useDisabledPageLinks <- website.useDisabledPageLinks,
		useFirstLastPageLinks <- website.useFirstLastPageLinks,
		firstPageLabel <- website.firstPageLabel,
		firstPageIconName <- website.firstPageIconName,
		lastPageLabel <- website.lastPageLabel,
		lastPageIconName <- website.lastPageIconName,
		paginationClass <- website.paginationClass,
		paginationElementClass <- website.paginationElementClass
	)
}


-- @extends SingletonUnit
abstract rule EditUnit {
	from website : Website!EditUnit
	to waf : WAF!EditUnit (
		disableWhen <- website.disableWhen,
		uriElement <-
			if website.uriElement.trim() <> '' then
				website.uriElement.trim()
			else if website.createDefaultUriElement then
				'update'
			else
				''
			endif endif,
			confirmDestination <- website.confirmDestination,
		confirmLabel <-
			if not website.confirmLabel.oclIsUndefined() and website.confirmLabel <> '' then
				website.confirmLabel
			else
				if  website.oclIsTypeOf(Website!CreateUnit) then
					'Create'
				else if  website.oclIsTypeOf(Website!CreateUpdateUnit) then
					'Save'
				else if  website.oclIsTypeOf(Website!UpdateUnit) then
					'Update'
				else if  website.oclIsTypeOf(Website!MapUnit) then
					'Save'
				else
					'Edit'
				endif endif endif endif
			endif,
		cancelDestination <- website.cancelDestination,
		cancelLabel <-
			if not website.cancelLabel.oclIsUndefined() and website.cancelLabel <> '' then
				website.confirmLabel
			else
				'Cancel'
			endif,
		customiseValues <- website.customiseValues
	)
}

-- @extends EditUnit
rule CreateUnit {
	from website : Website!CreateUnit
	to waf : WAF!CreateUnit (
		uriElement <-
			if website.uriElement.trim() <> '' then
				website.uriElement.trim()
			else if website.createDefaultUriElement then
				'create'
			else
				''
			endif endif
	)
}

-- @extends EditUnit, SelectableUnit
rule CreateUpdateUnit {
	from website : Website!CreateUpdateUnit
	to waf : WAF!CreateUpdateUnit (
		onSaveContinueEditing <- website.onSaveContinueEditing,
		createUriElement <-
			if website.createUriElement.trim() <> '' then
				website.createUriElement.trim()
			else
				'create'
			endif,
		clearLabel <- website.clearLabel
	)
}

-- @extends EditUnit, SelectableUnit
rule MapUnit {
	from website : Website!MapUnit
	to waf : WAF!MapUnit (
		readOnly <- website.readOnly,
		defaultZoomLevel <- website.defaultZoomLevel,
		location <- website.location.findOrmAttribute(),
		placeName <-
			if website.placeName.oclIsUndefined() then
				website.placeName
			else
				website.placeName.findOrmAttribute()
			endif
	)
}

-- @extends EditUnit, SelectableUnit
rule UpdateUnit {
	from website : Website!UpdateUnit
	to waf : WAF!UpdateUnit (
		onSaveContinueEditing <- website.onSaveContinueEditing
	)
}

-- @extends SingletonUnit, SelectableUnit
rule DetailsUnit {
	from website : Website!DetailsUnit
	to waf : WAF!DetailsUnit (
		selection <-
			if website.selection.oclIsUndefined() then
				website.selection
			else
				website.selection.findServiceSelection()
			endif,
		omitFieldLabels <- website.omitFieldLabels
	)
}

-- @extends CollectionUnit, InlineActionContainer
abstract rule IndexUnit {
	from website : Website!IndexUnit
	to waf : WAF!IndexUnit (
		omitColumnLabels <- website.omitColumnLabels,
		rowClasses <- website.rowClasses.replaceAll(',', ' ')
	)
}

-- @extends IndexUnit
rule IndexGridUnit {
	from website : Website!IndexUnit (website.displayOption = #Grid)
	to waf : WAF!IndexGridUnit (
	)
}

-- @extends IndexUnit
rule IndexPageDirectionUnit {
	from website : Website!IndexUnit (website.displayOption = #PageDirection)
	to waf : WAF!IndexPageDirectionUnit (
	)
}

-- @extends IndexUnit
rule IndexLineDirectionUnit {
	from website : Website!IndexUnit (website.displayOption = #LineDirection)
	to waf : WAF!IndexLineDirectionUnit (
	)
}

-- @extends CollectionUnit, InlineActionContainer
abstract rule CardsUnit {
	from website : Website!CardsUnit
	to waf : WAF!CardsUnit (
		omitFieldLabels <- website.omitFieldLabels,
		overlaySingleSelectAction <- website.overlaySingleSelectAction,
		columnClass <- website.columnClass.replaceAll(',', ' ').trim(),
		cardClass <- website.cardClass.replaceAll(',', ' ').trim()
	)
}

-- @extends CardsUnit
rule CardsIndexUnit {
	from website : Website!CardsIndexUnit
	to waf : WAF!CardsIndexUnit (
	)
}

-- @extends CollectionUnit
abstract rule ImageUnit {
	from website : Website!ImageUnit
	to waf : WAF!ImageUnit (
		imagePathFeature <- website.imagePathFeature,
		imageFilter <- website.imageFilter,
		missingImageFilter <- website.missingImageFilter,
		missingImagePath <- website.missingImagePath
	)
}

-- @extends CardsUnit, ImageUnit
rule ImageIndexUnit {
	from website : Website!ImageIndexUnit
	to waf : WAF!ImageIndexUnit (
		enableImageEnlargement <- website.enableImageEnlargement,
		overlayTitle <- website.overlayTitle,
		revealUntruncatedContent <- website.revealUntruncatedContent
	)
}

-- @extends ImageUnit
rule SliderUnit {
	from website : Website!SliderUnit
	to waf : WAF!SliderUnit (
		showTime <- website.showTime,
		transitionTime <- website.transitionTime
	)
}

-- @extends ImageUnit
rule GalleryUnit {
	from website : Website!GalleryUnit
	to waf : WAF!GalleryUnit (
		fullSizeFilter <- website.fullSizeFilter,
		showTime <- website.showTime,
		transitionTime <- website.transitionTime
	)
}

-- @extends DynamicUnit
abstract rule ControlUnit {
	from website : Website!ControlUnit
	to waf : WAF!ControlUnit (
		submitLabel <-
			if not website.submitLabel.oclIsUndefined() and website.submitLabel <> '' then
				website.submitLabel
			else
				if  website.oclIsKindOf(Website!SearchUnit) then
					'Search'
				else if  website.oclIsKindOf(Website!RegistrationUnit) then
					'Register'
				else if  website.oclIsKindOf(Website!LoginUnit) then
					'Login'
				else if  website.oclIsKindOf(Website!ForgottenPasswordUnit) then
					'Forgotton Password'
				else
					'Control'
				endif endif endif endif
			endif,
		cancelDestination <- website.cancelDestination,
		cancelLabel <- website.cancelLabel
	)
}

-- @extends ControlUnit
rule SearchUnit {
	from website : Website!SearchUnit
	to waf : WAF!SearchUnit (
		resultsDestination <- website.resultsDestination
	)
}

-- @extends EditUnit
rule RegistrationUnit {
	from website : Website!RegistrationUnit
	to waf : WAF!RegistrationUnit (
	)
}

-- @extends ControlUnit
rule LoginUnit {
	from website : Website!LoginUnit
	to waf : WAF!LoginUnit (
		loginIdField <- website.loginIdField,
		passwordField <- website.passwordField,
		rememberMeField <- website.rememberMeField,
		logoutUriElement <- website.logoutUriElement
	)
}

-- @extends ControlUnit
rule ForgottenPasswordUnit {
	from website : Website!ForgottenPasswordUnit
	to waf : WAF!ForgottenPasswordUnit (
		loginIdField <- website.loginIdField
	)
}


abstract rule InlineActionContainer {
	from website : Website!InlineActionContainer
	to waf : WAF!InlineActionContainer (
		actions <- website.actions
	)
}

-- @extends NamedDisplayElement
abstract rule InlineAction {
	from website : Website!InlineAction
	to waf : WAF!InlineAction (
		disable <- website.disable,
		displayWhen <- website.displayWhen,
		header <- website.header,
		footer <- website.footer,
		headerClass <- website.headerClass,
		footerClass <- website.footerClass
	)
}


-- @extends InlineAction
rule SelectAction {
	from website : Website!SelectAction
	to waf : WAF!SelectAction (
		target <- website.target,
		valuePath <- website.valuePath
	)
}

-- @extends InlineAction
rule DeleteAction {
	from website : Website!DeleteAction 
	to waf : WAF!DeleteAction (
		destination <- website.destination,
		confirmMessage <- 
			if not website.confirmMessage.oclIsUndefined() then
				website.confirmMessage
			else if not website.displayLabel.oclIsUndefined() then
				website.displayLabel.concat('?')
			else
				website.name.createLabel().concat('?')
			endif endif,
		requiresRole <-
			if website.requiresRole.trim() <> '' then
				website.requiresRole.trim()
			else
				website.usedBy.requiresRole.trim()
			endif,
		enableWhen <- website.enableWhen,
		uriElement <-
			if website.uriElement <> '' then
				website.uriElement
			else
				website.name.createName()
			endif
	)
}

-- @extends InlineAction
rule FeatureSupportAction {
	from website : Website!FeatureSupportAction 
	to waf : WAF!FeatureSupportAction (
		operation <- website.operation.findBusinessOperation(),
		confirmMessage <-
			if not website.confirmMessage.oclIsUndefined() and website.confirmMessage <> '' then
				website.confirmMessage
			else
				OclUndefined
			endif,
		requiresRole <-
			if website.requiresRole.trim() <> '' then
				website.requiresRole.trim()
			else
				website.usedBy.requiresRole.trim()
			endif,
		enableWhen <- website.enableWhen,
		uriElement <-
			if website.uriElement <> '' then
				website.uriElement
			else
				website.name.createName()
			endif,
		fileExtension <- website.fileExtension
	)
}


rule Badge {
	from website : Website!Badge
	to waf : WAF!Badge (
		iconName <- website.iconName,
		value <- website.value,
		displayWhen <- website.displayWhen,
		newWhen <- website.newWhen
	)
}

rule ConstantReference {
	from website : Website!ConstantReference (website.isInterfaceExpression())
	to waf : WAF!ConstantReference (
		value <-
			if website.value.oclIsUndefined() then
				website.value
			else
				website.value.findServiceConstant()
			endif
	)
}
