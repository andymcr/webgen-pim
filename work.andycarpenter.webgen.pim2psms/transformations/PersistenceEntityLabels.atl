-- @atlcompiler emftvm 
--
-- @path Website=/uk.ac.man.cs.mdsd.webgen.model/model/website.ecore
-- @path ORM=/uk.ac.man.cs.mdsd.orm.model/model/orm.ecore

module PersistenceEntityLabels;
create dummyOrm : ORM refining orm : ORM, website : Website;

uses WebsiteHelpers;


abstract rule NamedElement {
	from website : Website!NamedElement
	to orm : ORM!NamedElement (
		name <- website.name
	)
}


-- @extends NamedElement
rule ModelLabel {
	from website : Website!ModelLabel
	to orm : ORM!ModelLabel (
		format <- website.format,
		customise <- website.customise,
		features <- website.features
	)
}

abstract rule ModelLabelFeature {
	from website : Website!ModelLabelFeature
	to orm : ORM!ModelLabelFeature (
	)
}

-- @extends ModelLabelFeature
rule ModelLabelAttribute {
	from website : Website!ModelLabelAttribute
	to orm : ORM!ModelLabelAttribute (
		attribute <-
			if website.attribute.oclIsKindOf(Website!Attribute) then
				website.attribute.findOrmAttribute()
			else
				website.attribute
			endif,
		dateFormat <-
			if not website.dateFormat.oclIsUndefined() and website.dateFormat <> '' then
				website.dateFormat
			else
				website.attribute.dateFormat()
			endif
	)
}

-- @extends ModelLabelFeature
rule ModelLabelAssociation {
	from website : Website!ModelLabelAssociation
	to orm : ORM!ModelLabelAssociation (
		association <-
			if website.association.oclIsUndefined() then
				website.association
			else
				if website.isSourceAssociation then
					website.association.findOrmEntity().getOrmFeature(website.association.name)
				else
					website.association.findOrmTargetEntity().getOrmFeature(website.association.targetFeatureName)
				endif
			endif,
		dynamicLabel <- website.dynamicLabel
	)
}


rule Entity {
	from partialOrm : ORM!Entity
	to orm : ORM!Entity (
		labels <-
			let website : Website!Entity
				= thisModule.findWebsiteEntity(partialOrm.name)
				in if website.labels->exists(l | l.name = 'default') then
						website.labels
					else
						website.labels-> union(
							if partialOrm.features->exists(f | f.name = 'name') then
								Sequence{thisModule.implicitEntityLabel(
									partialOrm.features->any(f | f.name = 'name'))
								}
							else if partialOrm.features->exists(f | f.name = 'title') then
								Sequence{thisModule.implicitEntityLabel(
									partialOrm.features->any(f | f.name = 'title'))
								}
							else
								Sequence{}
							endif endif)
					endif
	)
}

lazy rule implicitEntityLabel {
	from website : ORM!Feature
	to orm : ORM!ModelLabel (
		name <- 'default',
		format <- '%s',
		features <- Sequence{feature}
	), feature : ORM!ModelLabelAttribute (
		attribute <- website
	)
	do {
		orm;
	}
}


