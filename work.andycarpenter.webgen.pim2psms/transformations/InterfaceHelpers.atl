-- @atlcompiler emftvm

library InterfaceHelpers;

uses WebsiteHelpers;


helper context Website!DeleteAction def: confirmMessage() : String
	= if not self.confirmMessage.oclIsUndefined() and self.confirmMessage.trim() <> '' then
				self.confirmMessage.trim()
			else if not self.displayLabel.oclIsUndefined() and self.displayLabel.trim() <> '' then
				self.displayLabel.trim().concat('?')
			else
				self.name.createLabel().concat('?')
			endif endif;

helper context Website!Feature def: defaultDisplayValue() : String
	= if self.defaultDisplayValue.trim() <> '' then
				self.defaultDisplayValue.trim()
			else
				OclUndefined
			endif;

helper context Website!UnitAssociation def: defaultDisplayValue() : String
	= if self.defaultDisplayValue.trim() <> '' then
			self.defaultDisplayValue.trim()
		else
			self.association.defaultDisplayValue()
		endif;

helper context Website!UnitElement def: defaultDisplayValue() : String
	= if self.defaultDisplayValue.trim() <> '' then
			self.defaultDisplayValue.trim()
		else
			self.attribute.defaultDisplayValue()
		endif;

helper context Website!Feature def: displayClass() : Boolean
	= if self.displayClass.trim() <> '' then
			self.displayClass.replaceAll(',', ' ').trim()
		else
			OclUndefined
		endif;

helper context Website!UnitAssociation def: displayClass() : Boolean
	= if self.displayClass.trim() <> '' then
			self.displayClass.replaceAll(',', ' ').trim()
		else
			self.association.displayClass()
		endif;

helper context Website!UnitElement def: displayClass() : Boolean
	= if self.displayClass.trim() <> '' then
			self.displayClass.replaceAll(',', ' ').trim()
		else
			self.attribute.displayClass()
		endif;

helper context Website!Feature def: displayLabel() : String
	= if self.displayLabel.trim() <> '' then
			self.displayLabel.trim()
		else
			self.name.createLabel()
		endif;

helper context Website!Association def: targetDisplayLabel() : String
	= if self.targetDisplayLabel.trim() <> '' then
			self.targetDisplayLabel.trim()
		else
			self.targetFeatureName().createLabel()
		endif;

helper context Website!UnitAssociation def: displayLabel() : String
	= if self.displayLabel.trim() <> '' then
			self.displayLabel.trim()
		else
			if self.childFeature.oclIsUndefined() then
				if self.isSourceAssociation then
					self.association.displayLabel()
				else
					self.association.targetDisplayLabel()
				endif 
			else
				self.childFeature.displayLabel()
			endif
		endif;

helper context Website!UnitElement def: displayLabel() : String
	= if self.displayLabel.trim() <> '' then
			self.displayLabel.trim()
		else
			self.attribute.displayLabel()
		endif;

helper context Website!UnitLabel def: displayLabel() : String
	= OclUndefined;

helper context Website!ChildPathAssociation def: displayLabel() : String
	= if self.childFeature.oclIsUndefined() then
				if self.isSourceAssociation then
					self.association.displayLabel()
				else
					self.association.targetDisplayLabel()
				endif 
			else
				self.childFeature.displayLabel()
			endif;

helper context Website!ChildPathAttribute def: displayLabel() : String
	= self.attribute.displayLabel();

helper context Website!Feature def: emptyDisplayValue() : String
	= if self.emptyDisplayValue.trim() <> '' then
			self.emptyDisplayValue.trim()
		else
			OclUndefined
		endif;

helper context Website!UnitAssociation def: emptyDisplayValue() : String
	= if self.emptyDisplayValue.trim() <> '' then
			self.emptyDisplayValue.trim()
		else
			self.association.emptyDisplayValue()
		endif;

helper context Website!UnitElement def: emptyDisplayValue() : String
	= if self.emptyDisplayValue.trim() <> '' then
			self.emptyDisplayValue.trim()
		else
			self.attribute.emptyDisplayValue()
		endif;

helper context Website!Expression def: findContainingSelection() : Website!Selection
	= if self.refImmediateComposite().oclIsUndefined() then
			OclUndefined
		else if self.refImmediateComposite().oclIsTypeOf(Website!Selection) then
			self.refImmediateComposite()
		else if self.refImmediateComposite().oclIsKindOf(Website!Expression) then
			self.refImmediateComposite().findContainingSelection()
		else
			OclUndefined
		endif endif endif ;

helper context Website!Association def: findOrmEntity(entity : Website!Entity) : ORM!Entity
	= if entity.features->includes(self) then
			thisModule.findOrmEntity(entity.name)
		else
			thisModule.findOrmEntity(self.targetEntity.name)
		endif;

helper context Website!Attribute def: findOrmLabel() : ORM!Label
	= self.findOrmEntity().getOrmFeature(self.name);

helper context Website!ModelLabel def: findOrmLabel() : ORM!Label
	= thisModule.findOrmEntity(self.labelFor.name).getOrmLabel(self.name);

helper context Website!Feature def: footerClass() : Boolean
	= if self.footerClass.trim() <> '' then
			self.footerClass.replaceAll(',', ' ').trim()
		else
			OclUndefined
		endif;

helper context Website!UnitAssociation def: footerClass() : Boolean
	= if self.footerClass.trim() <> '' then
			self.footerClass.replaceAll(',', ' ').trim()
		else
			self.association.footerClass()
		endif;

helper context Website!UnitElement def: footerClass() : Boolean
	= if self.footerClass.trim() <> '' then
			self.footerClass.replaceAll(',', ' ').trim()
		else
			self.attribute.footerClass()
		endif;

helper context Website!InterfaceField def: hasCustomisedDisplayLabel() : Boolean
	= true;

helper context Website!UnitAssociation def: hasCustomisedDisplayLabel() : Boolean
	= if self.childFeature.oclIsUndefined() then
			if self.isSourceAssociation then
				self.displayLabel() <> self.association.displayLabel()
			else
				self.displayLabel() <> self.association.targetDisplayLabel()
			endif
		else
			true
		endif;

helper context Website!UnitElement def: hasCustomisedDisplayLabel() : Boolean
	= self.displayLabel() <> self.attribute.displayLabel();

helper context Website!UnitLabel def: hasCustomisedDisplayLabel() : Boolean
	= false;

helper context Website!Feature def: headerClass() : Boolean
	= if self.headerClass.trim() <> '' then
			self.headerClass.replaceAll(',', ' ').trim()
		else
			OclUndefined
		endif;

helper context Website!UnitAssociation def: headerClass() : Boolean
	= if self.headerClass.trim() <> '' then
			self.headerClass.replaceAll(',', ' ').trim()
		else
			self.association.headerClass()
		endif;

helper context Website!UnitElement def: headerClass() : Boolean
	= if self.headerClass.trim() <> '' then
			self.headerClass.replaceAll(',', ' ').trim()
		else
			self.attribute.headerClass()
		endif;

helper context Website!UnitField def: inputGroupClass() : Boolean
	= if self.inputGroupClass.trim() <> '' then
			self.inputLabelClass.trim()
		else if thisModule.findWebsiteUI().defaultInputGroupClass.trim() <> '' then
			thisModule.findWebsiteUI().defaultInputGroupClass
		else
			OclUndefined
		endif endif;

helper context Website!UnitField def: inputLabelClass() : Boolean
	= if self.inputLabelClass.trim() <> '' then
			self.inputLabelClass.trim()
		else if thisModule.findWebsiteUI().defaultInputLabelClass.trim() <> '' then
			thisModule.findWebsiteUI().defaultInputLabelClass
		else
			OclUndefined
		endif endif;

helper context Website!UnitField def: inputElementClass() : Boolean
	= if self.inputElementClass.trim() <> '' then
			self.inputElementClass.trim()
		else if thisModule.findWebsiteUI().defaultInputElementClass.trim() <> '' then
			thisModule.findWebsiteUI().defaultInputElementClass
		else
			OclUndefined
		endif endif;

helper context Website!Attribute def: interfaceType() : String
	= if self.oclIsTypeOf(Website!DataTypeAttribute) then
			if self.interfaceType.trim() <> '' then
				self.interfaceType.trim()
			else
				if self.oclIsTypeOf(Website!DataTypeAttribute) then
					if self.dataType.interfaceType.trim() <> '' then
						self.dataType.interfaceType.trim()
					else
						self.name.lowercaseFirst()
					endif
				else
					self.name.lowercaseFirst()
				endif
			endif
		else if self.oclIsTypeOf(Website!DateAttribute) then
			if self.details = #DateAndTime then
				'datetime'
			else if self.details = #DateOnly then
				'date'
			else
				'time'
			endif endif
		else
			OclUndefined
		endif endif;

helper context Website!DataTypeField def: interfaceType() : String
	= if self.interfaceType.trim() <> '' then
			self.interfaceType.trim()
		else
			if self.dataType.interfaceType.trim() <> '' then
				self.dataType.interfaceType.trim()
			else
				self.name.lowercaseFirst()
			endif
		endif;

helper context Website!Feature def: placeholder() : String
	= if self.placeholder.trim() <> '' then
			self.placeholder.trim()
		else
			self.name.createLabel()
		endif;

helper context Website!UnitElement def: placeholder() : String
	= if self.placeholder.trim() <> '' then
			self.placeholder.trim()
		else
			self.attribute.placeholder()
		endif;

helper context Website!Feature def: title() : String
	= if self.title.trim() <> '' then
			self.title.trim()
		else
			self.name.createLabel()
		endif;

helper context Website!UnitAssociation def: title() : String
	= if self.title.trim() <> '' then
			self.title.trim()
		else
			if self.isSourceAssociation then
				self.association.title()
			else
				self.association.targetDisplayLabel()
			endif
		endif;

helper context Website!UnitElement def: title() : String
	= if self.title.trim() <> '' then
			self.title.trim()
		else
			self.attribute.title()
		endif;

helper context Website!UnitLabel def: title() : String
	= OclUndefined;

helper context Website!InterfaceField def: validationPattern() : String
	= if self.validationPattern.trim() <> '' then
			self.validationPattern.trim()
		else
			if self.oclIsTypeOf(Website!DataTypeField) then
				self.dataType.validationPattern.trim()
			else
				OclUndefined
			endif
		endif;

helper context Website!InlineActionContainer def: actionNavigationClass() : String
	= if self.actionNavigationClass.trim() <> '' then
			self.actionNavigationClass.trim()
		else if thisModule.findWebsiteUI().defaultActionNavigationClass.trim() <> '' then
			thisModule.findWebsiteUI().defaultActionNavigationClass
		else
			OclUndefined
		endif endif;

helper context Website!InlineAction def: anchorClass() : String
	= if self.anchorClass.trim() <> '' then
			self.anchorClass.trim()
		else if self.refImmediateComposite().defaultLocalAnchorClass.trim() <> '' then
			self.refImmediateComposite().defaultLocalAnchorClass.trim()
		else if thisModule.findWebsiteUI().defaultAnchorClass.trim() <> '' then
			thisModule.findWebsiteUI().defaultAnchorClass
		else
			OclUndefined
		endif endif endif;

